buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
  }
}

plugins {
  id 'java'
  id 'application'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id "com.google.protobuf" version "0.8.12"
}

group = 'fr.pierrezemb'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  vertxVersion = '3.8.5'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

sourceCompatibility = '11'

def mainVerticleName = 'fr.pierrezemb.recordstore.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-junit5:$vertxVersion"
  implementation "io.vertx:vertx-grpc:$vertxVersion"
  implementation 'org.foundationdb:fdb-record-layer-core-pb3:2.8.91.0'
  compile "io.vertx:vertx-micrometer-metrics:$vertxVersion"
  compile "io.micrometer:micrometer-registry-prometheus:1.4.2"

  compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testCompile "org.testcontainers:testcontainers:1.13.0"

  // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
  testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'


  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}


shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
  dependsOn cleanTest
  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.2.0'
  }
  plugins {
    grpc {
      artifact = "io.vertx:protoc-gen-grpc-java:1.25.0"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
}
